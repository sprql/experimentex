defmodule ProteinTranslation do
  @codons %{
    "UGU" => "Cysteine",
    "UGC" => "Cysteine",
    "UUA" => "Leucine",
    "UUG" => "Leucine",
    "AUG" => "Methionine",
    "UUU" => "Phenylalanine",
    "UUC" => "Phenylalanine",
    "UCU" => "Serine",
    "UCC" => "Serine",
    "UCA" => "Serine",
    "UCG" => "Serine",
    "UGG" => "Tryptophan",
    "UAU" => "Tyrosine",
    "UAC" => "Tyrosine",
    "UAA" => "STOP",
    "UAG" => "STOP",
    "UGA" => "STOP"
  }

  @doc """
  Given an RNA string, return a list of proteins specified by codons, in order.
  """
  @spec of_rna(String.t()) :: { atom,  list(String.t()) }
  def of_rna(rna) do
    list =
      rna
      |> String.splitter("", trim: true)
      |> Stream.chunk(3)
      |> Enum.reduce_while([], &(translate_sequence(&1, &2)))

    case list do
      [:error] -> {:error, "invalid RNA"}
      list -> {:ok, Enum.reverse(list)}
    end
  end

  defp translate_sequence(sequence, acc) do
    codon = Enum.join(sequence)
    case of_codon(codon) do
      {:ok, protein} -> if protein == "STOP", do: {:halt, acc}, else: {:cont, [protein | acc]}
      {:error, _} -> {:halt, [:error]}
    end
  end

  @doc """
  Given a codon, return the corresponding protein

  UGU -> Cysteine
  UGC -> Cysteine
  UUA -> Leucine
  UUG -> Leucine
  AUG -> Methionine
  UUU -> Phenylalanine
  UUC -> Phenylalanine
  UCU -> Serine
  UCC -> Serine
  UCA -> Serine
  UCG -> Serine
  UGG -> Tryptophan
  UAU -> Tyrosine
  UAC -> Tyrosine
  UAA -> STOP
  UAG -> STOP
  UGA -> STOP
  """
  @spec of_codon(String.t()) :: { atom, String.t() }
  def of_codon(codon) do
    case Map.fetch(@codons, codon) do
      {:ok, protein} -> {:ok, protein}
      :error -> {:error, "invalid codon"}
    end
  end
end

